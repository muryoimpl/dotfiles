# -*- sh -*-

typeset -U path
path=(
      /bin(N-/)
      $HOME/local/bin(N-/)
      /opt/local/bin(N-/)
      /usr/local/bin(N-/)
      /usr/bin(N-/))

# sudo時のパスの設定
typeset -xT SUDO_PATH sudo_path
## 重複したパスを登録しない。
typeset -U sudo_path
sudo_path=({,/usr/local,/usr}/sbin(N-/))

# man用パスの設定
typeset -U manpath
manpath=(# 自分用
         $HOME/local/share/man(N-/)
         /opt/local/share/man(N-/)
         /usr/local/share/man(N-/)
         /usr/share/man(N-/))

# Rubyの設定
typeset -xT RUBYLIB ruby_path
### 重複したパスを登録しない。
typeset -U ruby_path
ruby_path=(# カレントディレクトリのライブラリを優先する
           ~/lib)

# ページャの設定
if type lv > /dev/null 2>&1; then
    ## lvを優先する。
    export PAGER="lv"
else
    ## lvがなかったらlessを使う。
    export PAGER="less"
fi

# lvの設定
if [ "$PAGER" = "lv" ]; then
    ## -c: ANSIエスケープシーケンスの色付けなどを有効にする。
    ## -l: 1行が長くと折り返されていても1行として扱う。
    ##     （コピーしたときに余計な改行を入れない。）
    export LV="-c -l"
else
    ## lvがなくてもlvでページャーを起動する。
    alias lv="$PAGER"
fi

# エディタの設定
## vimを使う。
export EDITOR=vim
## vimがなくてもvimでviを起動する。
if ! type vim > /dev/null 2>&1; then
    alias vim=vi
fi
