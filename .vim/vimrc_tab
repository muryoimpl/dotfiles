"tabの設定
"new tab
nnoremap <Space>tn :<C-u>tabnew<CR>
nnoremap <Space>tc :<C-u>tabclose<CR>

nnoremap <Tab><Tab> gt
for i in range(1, 9)
    execute 'nnoremap <Tab>' . i . ' ' . i . 'gt'
endfor
"-------- タブ表示 ---------------------------------
" http://d.hatena.ne.jp/thinca/20111204/1322932585 から拝借
set showtabline=2

"各タブページのカレントバッファ名+α を表示
function! s:tabpage_label(n)
  "t:titleがあればそれを使う
  let title = gettabvar(a:n, 'title')
  if title !=#''
    return title
  endif

  "タブページ内のバッファリスト
  let bufnrs = tabpagebuflist(a:n)
  "カレントページかどうかでハイライト切り替え
  let hi = a:n is tabpagenr() ? '%#TabLineSel#' : '%#TabLine#'

  "バッファ複数あったらバッファ数を表示
  let no = len(bufnrs)
  if no is 1
    let no = ''
  endif

  "タブページ内に変更ありのバッファがあれば'+'を表示
  let mod = len(filter(copy(bufnrs), 'getbufvar(v:val, "&modified")')) ? '+' : ''
  let sp = (no.mod) ==#'' ? '' : ' '

  "カレントバッファ
  let curbufnr = bufnrs[tabpagewinnr(a:n) - 1]
  let fname = pathshorten(bufname(curbufnr))

  let label = no.mod.sp.fname

  return '%'.a:n.'T'.hi.label.'%T%#TabLineFill#'
endfunction

function! MakeTabLine()
  let titles=map(range(1, tabpagenr('$')),'s:tabpage_label(v:val)')
  let sep=' †  ' "タブ区切り文字
  let tabpages=join(titles, sep).sep.'%#TabLineFill#%T'
  let info = fnamemodify(getcwd(),":~").'' "カレントdir表示
  return tabpages .'%='.info "タブを左、情報を右に
endfunction

set tabline=%!MakeTabLine()
"-------------------------------------------------------------

